plugins {
    id 'java-library'
    id "io.qameta.allure" version "2.9.6"
}

repositories {
    mavenCentral()
}

def allureVersion = "2.17.3",
    allureRestVersion = "2.21.0",
    appiumVersion = "8.3.0",
    restAssured = "4.5.1",
    junitVersion = "5.8.2",
    slf4jVersion = "1.7.32",
    selenideVersion = "6.11.0",
    ownerVersion = "1.0.4",
    commonVersion = "2.11.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.qameta.allure:allure-rest-assured:$allureRestVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.appium:java-client:$appiumVersion",
            "io.rest-assured:rest-assured:$restAssured",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "commons-io:commons-io:$commonVersion",
            "org.aeonbits.owner:owner:$ownerVersion"
    )
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    environment "deviceHost", "emulation"
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task android_test(type: Test) {
    environment "deviceHost", "browserstack"
    useJUnitPlatform {
        includeTags 'android'
    }
}

task ios_test(type: Test) {
    environment "deviceHost", "browserstack"
    useJUnitPlatform {
        includeTags 'ios'
    }
}

task emulation_test(type: Test) {
    environment "deviceHost", "emulation"
    useJUnitPlatform {
        includeTags 'emulation'
    }
}